#!/usr/bin/env perl
# Use perldoc or option --man to read documentation
###################################################

our $VERSION = "00.00_01";
# Originally created: 11/18/10 20:47:49.

# Include core modules.
use strict;
use warnings;
use File::Basename;
use Carp;

# Include CPAN modules.
use Getopt::Euclid qw{ :vars<opt_> };

# Include private modules.
use What;
use What::WhatRC;
use What::Release;
use What::Subsystem;
use What::Context;
use What::Format;

########################################################################
# GENERAL GLOBAL VARIABLES
########################################################################

# Basic program information.
my $prog_incantation  = $0;
my $prog_dir          = dirname($0);
my $prog_name         = basename($0);

# Command-line option hash.
my %opt = %ARGV;

# Global program exitcode.
my $exitcode = 0;

########################################################################
# PROGRAM SPECIFIC GLOBALS
########################################################################

########################################################################
# MAIN FUNCTION
########################################################################

# main() - main program execution 
sub main{
    load_context();
    my $release = What::Release->new(
        artist => context->artist,
        title => context->title,
        year => context->year,
    );

    my $rname = $release->name();
    my $had_to_create_format = 0;
    my $format = uc whatrc->preferred_format;
    die "Format isn't $format recognized" if (!format_is_possible($format));
    my $file_type = file_format_of($format);
    die "Can't recognize the file type of $format" if !defined $file_type;
    my $format_arg = $file_type eq 'MP3' ? "--mp3 $format" : "--$format";

    die "There is no release named '$rname' in the upload hierarchy."
        if !$release->exists();

    if ( !$release->exists($format) ) {
        print "Creating temporary $format release for the library.\n";

        0 == subsystem(
            cmd => [
                'release-flac-convert', 
                '--artist', $release->{artist},
                '--title', $release->{title},
                '--year', $release->{year},
                $format_arg,
            ],
        ) or die "Couldn't create $format release!";

        $had_to_create_format = 1;
    }


    # List the possible configurations. and generate a status message.
    my $config1 = (whatrc->should_add_to_itunes && whatrc->itunes_copies_music);
    my $config2 = whatrc->should_add_to_itunes;
    # my $config3 = ...
    my $status 
        = $config1 ?"Adding the release to iTunes..."
        : $config2 ?"Copying the release to the library and adding to iTunes..."
        : "Copying the release to the library...";
    print "$status";

    # Add the release to the library.
    $release->copy_music_into_hierarchy( 
        format => $format, 
        library_root => whatrc->library,
        add_to_itunes => whatrc->should_add_to_itunes,
        itunes_will_copy => whatrc->itunes_copies_music,);
    print "Done\n";

    if ($had_to_create_format) {
        print "Deleting temporary $format release.\n";
        0 == subsystem(
            cmd => [
                'release-delete',
                '--artist', $release->{artist},
                '--title', $release->{title},
                '--year', $release->{year},
                $format_arg,
            ],
        ) or die "Couldn't remove temporary $format release!";
    }
    exit($exitcode);
}

########################################################################
# SUPPORTING SUBROUTINES
########################################################################

# Finally, kickstart the script
main();

__END__

=head1 NAME

release-add-to-library
-- Add a specified release to the user's music library.

=head1 VERSION

Version 0.0_1

=head1 USAGE

release-add-to-library [options]

=head1 REQUIRED ARGUMENTS

=over

=back

=head1 OPTIONS

=over

=item --artist <artist>

Release artist.

=for Euclid:
    artist.type: string, length artist

=item --title <title>

=for Euclid:
    title.type: string, length title

Release title.

=item --year <year>

Release year.

=for Euclid:
    year.type: /\d{4}/

=item --version

=item --usage

=item --help

=item --man

Print the usual program information

=back

=head1 DESCRIPTION

Add a specified release to the user's music library.

=head1 BUGS

=over

=back

=head1 COPYRIGHT 

(c) The What team

=cut
