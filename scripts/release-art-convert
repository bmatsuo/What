#!/usr/bin/env perl

# Use perldoc or option --man to read documentation

########################################################################
# MODULES
########################################################################

# Include core modules.
use strict;
use warnings;
use File::Basename;
use File::Glob 'bsd_glob';
use Carp;

# Include CPAN modules.
use Getopt::Euclid qw{ :vars<opt_> };
use Readonly;

# Include private modules.
use What;
use What::Release;
use What::WhatRC;

########################################################################
# GENERAL GLOBAL VARIABLES
########################################################################

# Basic program information.
Readonly my $prog_incantation  => $0;
Readonly my $prog_dir          => dirname($0);
Readonly my $prog_name         => basename($0);

# Command-line option hash.
my %opt = %ARGV;

# Global program exitcode.
my $exitcode = 0;

########################################################################
# PROGRAM SPECIFIC GLOBALS
########################################################################

my $whatrc = read_whatrc('~/.whatrc');

########################################################################
# MAIN FUNCTION
########################################################################

# main() - main program execution 
sub main{
    my $release = What::Release->new(
        artist => $opt{'--artist'},
        title => $opt{'--title'},
        year => $opt{'--year'},);
    my $release_name = $release->name();

    my $source_dir 
        = safe_path($release->format_dir($whatrc->{upload_root}, 'flac'));

    my @images = bsd_glob("$source_dir/*.tiff");
    my $dest = $opt{'--output-dir'};
    my $new_size = $opt{'--size'};
    my $new_format = $opt{'--format'};
    $dest 
        =~ s! \A ~ ( [^/]* ) ! 
            $1 ? (getpwnam($1))[7] 
            : ( $ENV{HOME} || $ENV{LOGDIR} || (getpwuid($>))[7]) !exms;

    for my $raw_image (@images) {
        my $output_file = basename($raw_image);
        $output_file =~ s/\. [^.]+ \z/ - $release_name.$new_format/xms;
        my $output_path = "$dest/$output_file";
        my @convert_cmd 
            = ('convert', $raw_image, '-resize', $new_size, $output_path);
        system (@convert_cmd) == 0
            or croak("Couldn't convert $raw_image.\n$?\n");
    }

    exit($exitcode);
}

########################################################################
# SUPPORTING SUBROUTINES
########################################################################


# Finally, kickstart the script
main();

__END__

=head1 NAME

convert-art
-- convert high-res releas art to low-res versions.

=head1 VERSION

Version 0.0_2
Originally created on 07/13/10 23:48:53

=head1 USAGE

convert-art [options]

=head1 REQUIRED ARGUMENTS

=over

=item --artist <artist>

Release artist.

=for Euclid:
    artist.type: string, length artist

=item --title <title>

=for Euclid:
    title.type: string, length title

Release title.

=item --year <year>

Release year.

=for Euclid:
    year.type: /\d{4}/

=back

=head1 OPTIONS

=over

=item --output-dir <dest> | -o <dest>

Specify directory in which to output converted images. 
(Default: '~/Downloads')

=for Euclid:
    dest.default: '~/Downloads'
    dest.type: writable, -d dest

=item --size [=] <size>

Specify converted image size in pixels.
(Default: '500x500')

=for Euclid:
    size.default: '500x500'
    size.type: /\d+x\d+/

=item --format [=] <format> | -f <format>

Specify output format.
(Default: 'jpg')

=for Euclid:
    format.default: 'jpg'
    format.type: /.+/

=item --version

=item --usage

=item --help

=item --man

Print the usual program information

=back

=head1 DESCRIPTION

Easily convert cover art from high-resolution TIFF images to other
formats of a smaller size. Ideally, this converted image is then used for
either attaching to mp3 files or uploading to a host like imageshack.

=head1 EXAMPLES

The following command,

    > convert-art --artist M.I.A. --year 2010 --title Maya

Converts files matching the pattern
<RIP ROOT>/M.I.A./M.I.A. - 2010 - Maya/M.I.A. - 2010 - Maya [FLAC]/*.tiff
producing files of the form
~/Downloads/* - M.I.A. - 2010 - Maya.jpg
which are 500x500 pixel jpeg images.

The command,

    > convert-art --size 400x400 --format png --output-dir . \
        --artist M.I.A. --year 2010 --title Maya

produces 400x400 pixel PNG images in the current working directory.

=head1 BUGS

=over

=back

=head1 COPYRIGHT & LICENSE

Â©Bryan Matsuo (bryan.matsuo@gmail.com)

This file is part of What.

What is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

What is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with What.  If not, see <http://www.gnu.org/licenses/>.

=cut
