#!/usr/bin/env perl

# Use perldoc or option --man to read documentation

########################################################################
# MODULES
########################################################################

# Include core modules.
use strict;
use warnings;
use File::Basename;
use File::Glob 'bsd_glob';
use Carp;

# Include CPAN modules.
use Getopt::Euclid qw{ :vars<opt_> };
use Readonly;

# Include private modules.
use What::Release;
use What::WhatRC;
use What::Utils;
use What::Release::Directory;

########################################################################
# GENERAL GLOBAL VARIABLES
########################################################################

# Basic program information.
Readonly my $prog_incantation  => $0;
Readonly my $prog_dir          => dirname($0);
Readonly my $prog_name         => basename($0);

# Command-line option hash.
my %opt = %ARGV;

# Global program exitcode.
my $exitcode = 0;

########################################################################
# PROGRAM SPECIFIC GLOBALS
########################################################################

my $illegal_chars = qr{([/?:])}xms;

########################################################################
# MAIN FUNCTION
########################################################################

# main() - main program execution 
sub main{
    my $rip_dir = whatrc->rip_dir;

    if (!-d $rip_dir) {
        print {\*STDERR} "Can't find rip directory $rip_dir.\n";
    }

    my $rip = scan_rip_dir();
    if ( !(uc $rip->file_format eq 'FLAC') ) {
        print {\*STDERR} "Rip directory contents are not FLAC.\n";
        $exitcode = 1;
    }
    else {
        if ($opt{'--verbose'}) {
            my $num_discs = scalar @{$rip->discs};
            my $num_songs = 0;
            for my $disc (@{$rip->discs}) {
                $num_songs += scalar @{$disc->songs};
            }
            print {\*STDERR} sprintf (
                    "%d FLAC files found across %d discs.\n",
                    $num_songs, $num_discs);
        }
    }

    my @all_files = find_hierarchy($rip_dir, 1);
    my @directories = grep {-d $_} @all_files;

    my @images = @{$rip->images};
    if (!@images){
        print {\*STDERR} "No images found.\n";
        $exitcode = 1;
    }
    if ($opt{'--verbose'}) {
        print {\*STDERR} sprintf "%d image(s) found.\n", scalar @images;
    }

    # Handles for finding discs missing files.
    my %good_disc;
    my @bad_discs;

    for my $disc (@{$rip->discs}) {
        if (!$disc->has_log()) {
            if (!$opt{'--no-log'}) {
                my $disc_log_err 
                    = $disc->name eq q{} ? "Release has no log file.\n"
                    : sprintf "Disc %s has no log file", $disc->name;
                print {\*STDERR} $disc_log_err;
                $exitcode = 1;
            }
        }
        if (!$disc->has_cue()) {
            if (!$opt{'--no-cue'}) {
                my $disc_cue_err 
                    = $disc->name eq q{} ? "Release has no cue file.\n"
                    : sprintf "Disc %s has no cue file", $disc->name;
                print {\*STDERR} $disc_cue_err;
                $exitcode = 1;
            }
        }
    }

    my $info_file = grep {m/ Info\.txt \z/xms} @{$rip->other_files};
    if (!defined $info_file) {
        print {\*STDERR}
            "Info.txt was not found the rip directory.\n";
        $exitcode = 1;
    }
    elsif ($opt{'--verbose'}) {
        print {\*STDERR} "Info.txt found.\n";
    }

    # Run over all the files to look for errors and unexpected files.
    for my $file ($rip->files, @directories) {
        my $name = basename($file);

        if ($name =~ m!\A\..*\z!xms) {
            print {\*STDERR} "Hidden file found; $name\n";
            $exitcode = 1;
        }

        # Check the file name for illegal characters.
        if ($name =~ $illegal_chars) {
            print {\*STDERR} "Illegal character $1 in filename '$name'.\n";
            $exitcode = 1;
        }
    }

    for my $other (@{$rip->other_files}, (map {@{$_->other_files}} @{$rip->discs})) {
        # Extract the file's extension
        my $ext = $other =~ m/ \. ([^.]+) \z/xms ? lc $1 : "";

        if ($other =~ m/Info\.txt\z/xms) {}
        # Any other files are not expected.
        # This may turn into an error at some point.
        elsif ($ext eq '') {
            print {\*STDERR} 
                "warning: Unexpected extension-less file $other found.\n";
        }
        else {
            print {\*STDERR} 
                "warning: Unexpected file of type $ext found; $other\n";
        }
    }

    if ($opt{'--verbose'}) {
        if (!$exitcode) {
            print {\*STDERR}
                "Release is ready to be moved and converted to other formats.\n";
        }
        else {
            print {\*STDERR} 
                "Release is NOT ready to be moved and converted.\n";
        }
    }

    exit($exitcode);
}

########################################################################
# SUPPORTING SUBROUTINES
########################################################################

# Subroutine: release_dir_is_complete($dir)
# Type: INTERFACE SUB
# Purpose: Check that all desired files are in a release directory.
# Returns: 
sub release_dir_is_complete {
    my $dir = shift;

    my $disc_dir_pattern = "$dir/CD*";

    my @discs = bsd_glob($disc_dir_pattern);

    my $is_multidisc = scalar @discs;

    push @discs, "" if !$is_multidisc;

    find_supplemental_files($dir);

    for my $disc_dir (@discs) { check_music_dir($disc_dir); }

    return;
}

sub find_supplemental_files {
    my $dir = shift;
    my $supplements_ok = 1;

    # Find release description text file, Info.txt.
    my $info = "$dir/Info.txt";

    if (!-e $info) {
        print {\*STDERR} "Can't find description $info.\n";
        $exitcode = 1;
        $supplements_ok = 0;
    }

    # Find images (JPEG, TIFF, PNG)
    my @images = grep {
        m/\. (jpg | tiff | png) \z/xms;
    } bsd_glob("$dir/*.*");

    if (!@images) {
        print {\*STDERR} "No images were found.\n";
        $exitcode = 1;
        $supplements_ok = 0;
    }

    return $supplements_ok;
}

sub check_music_dir {
    my $dir = shift;
    my $music_dir_ok = 1;

    my @flac_files = bsd_glob("$dir/*.flac");

    if (!@flac_files) {
        print {\*STDERR} "No FLAC audio files found in $dir.\n";
        $exitcode = 1;
        $music_dir_ok = 0;
    }

    my @log_files = bsd_glob("$dir/*.log");

    if (!@log_files) {
        print {\*STDERR} ($opt{'--no-log'} ? "Warning: " : q{}),
            "No rip log found in $dir.\n";
        $exitcode = 1 if !$opt{'--no-log'};
        $music_dir_ok = 0;
    }

    my @cue_files = bsd_glob("$dir/*.log");

    if (!@cue_files) {
        print {\*STDERR} ($opt{'--no-cue'} ? "Warning: " : q{}),
            "No CUE file found in $dir.\n";
        $exitcode = 1 if !$opt{'--no-cue'};
        $music_dir_ok = 0;
    }

    if (@cue_files > 1) {
        print {\*STDERR} "Multiple CUE files found in $dir.\n";
        $exitcode = 1;
        $music_dir_ok = 0;
    }

    return $music_dir_ok;
}

sub check_cue_file {
    my $cue_path = shift;
}

sub check_log_file {
    my $log_path = shift;
}

# Finally, kickstart the script
main();

__END__

=head1 NAME

release-ok
-- Check the rip directory for a full release.

=head1 VERSION

Version 0.0_1
Originally created on 07/18/10 23:19:19

=head1 USAGE

release-ok [options]

=head1 REQUIRED ARGUMENTS

=over

=back

=head1 OPTIONS

=over

=item --no-cue

Do not require the release to have a cue file. This is not recommended.

=item --no-log

Do not require the release to have a log file. 
This is not recommended for CD rips.

=item --verbose

=item --version

=item --usage

=item --help

=item --man

Print the usual program information

=back

=head1 DESCRIPTION

Check that a flac release directory contains everything that it should.

TODO:

Check the flac for 100%

=head1 BUGS

=over

=back

=head1 COPYRIGHT & LICENSE

Â© The What team.

This file is part of What.

What is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

What is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with What.  If not, see <http://www.gnu.org/licenses/>.

=cut
