#!/usr/bin/env perl

# Use perldoc or option --man to read documentation

########################################################################
# MODULES
########################################################################

# Include core modules.
use strict;
use warnings;
use File::Basename;
use Carp;

# Include CPAN modules.
use Getopt::Euclid qw{ :vars<opt_> };
use Readonly;

# Include private modules.
use What::Subsystem;
use What::WhatRC;
use What::Release;

########################################################################
# GENERAL GLOBAL VARIABLES
########################################################################

# Basic program information.
Readonly my $prog_incantation  => $0;
Readonly my $prog_dir          => dirname($0);
Readonly my $prog_name         => basename($0);

# Command-line option hash.
my %opt = %ARGV;

# Global program exitcode.
my $exitcode = 0;

########################################################################
# PROGRAM SPECIFIC GLOBALS
########################################################################

########################################################################
# MAIN FUNCTION
########################################################################

# main() - main program execution 
sub main{
    my $release = What::Release->new(
        artist => $opt{'--artist'},
        title => $opt{'--title'},
        year => $opt{'--year'},);

    my $subsystem = What::Subsystem->new(
        dryrun=>$opt{'--dry-run'},
        verbose=>$opt{'--verbose'});

    my %want_format = (
        'V0'    => $opt{'--v0'},
        'V2'    => $opt{'--v2'},
        '320'   => $opt{'--320'},
        'FLAC'  => $opt{'--flac'},
        'OGG'   => $opt{'--ogg'},
        'AAC'   => $opt{'--aac'},
    );

    my $announce = whatrc->announce;

    for my $format (keys %want_format) {
        next if not $want_format{$format};

        my $format_dir 
            = $release->format_dir($format);

        my @mktorrent_cmd = mktorrent_command(
            announce    => $announce,
            source_dir  => $format_dir,);

        if ($opt{'--verbose'}) {
            print "Making $format torrent..."
        }
        else {
            $subsystem->{redirect_to} = '/dev/null';
        }
        $subsystem->exec(@mktorrent_cmd) == 0
            or croak("Couldn't create torrent file.");
        if ($opt{'--verbose'}) {
            print "Done."
        }
        else {
            $subsystem->{redirect_to} = undef;
        }


        # Add the torrents to the watch directory.
        if ($opt{'--add-to-watch'}) {
            my $torrent_name = $format_dir;
            if ($torrent_name !~ s!/?\z!.torrent!xms) {
                print {\*STDERR} "Unexpected error before copying torrent file; ",
                    "'$torrent_name'. Attempting to continue.\n";
                next;
            }
            $subsystem->exec('cp', $torrent_name, whatrc->watch) == 0
                or croak("Couldn't copy torrent file.");
        }
    }

    exit($exitcode);
}

########################################################################
# SUPPORTING SUBROUTINES
########################################################################

# Subroutine: mktorrent_options(
#   announce => $url,
#   source_dir => $src_path)
# Type: INTERNAL UTILITY
# Purpose: Construct a list of options for mktorrent.
# Returns: A list of options.
sub mktorrent_options {
    my %arg = @_;
    my ($url, $src_path) = ($arg{announce}, $arg{source_dir});

    # Remove a trailing '/' from the directory being used.
    $src_path =~ s!(.)/\z!$1!xms;

    my $torrent_path = "$src_path.torrent";

    my @options = ('-p', '-a', $url, '-o', $torrent_path);

    return @options;
}

# Subroutine: 
#   mktorrent_command(
#       announce => $url,
#       source_dir => $src_path,)
# Type: INTERFACE SUB
# Purpose: Construct a system command that will create a 
# Returns: A list representation of the system command.
sub mktorrent_command {
    my %arg = @_;

    croak("Announce URL is not specified.")
        if not $arg{announce};
    croak("Torrent source is not specified.")
        if not $arg{source_dir};

    my @options = mktorrent_options(%arg);

    my @cmd = ('mktorrent', @options, $arg{source_dir});

    return @cmd;
}

# Finally, kickstart the script
main();

__END__

=head1 NAME

mktorrent-release
-- Create torrent files for any desired format release directory.

=head1 VERSION

Version 0.0_1
Originally created on 07/16/10 03:41:08

=head1 USAGE

mktorrent-release [options]

=head1 REQUIRED ARGUMENTS

=over

=item --artist <artist>

Release artist.

=for Euclid:
    artist.type: string, length artist

=item --title <title>

=for Euclid:
    title.type: string, length title

Release title.

=item --year <year>

Release year.

=for Euclid:
    year.type: /\d{4}/

=back

=head1 OPTIONS

=over

=item --v0 | --V0

Create an MP3 V0 VBR torrent.

=item --v2 | --V2

Create an MP3 V2 VBR torrent.

=item --320

Create an MP3 320kbps CBR torrent.

=item --flac | --FLAC

Create a FLAC torrent.
FLAC files will be stored in the upload hierachy 
regardless of this option.

=item --ogg | --OGG | --q8

Create an OGG -q8.0 VBR torrent.

=item --aac | --AAC

Create an AAC ~256kbps ABR torrent.

=item --all

Create torrents for all present formats and bitrates (not working yet).

=item -w | --add-to-watch

Copy newly created torrents to your clients watched directory.
This is configurable via the file '~/.whatrc'.

=item --dry-run

=item --verbose

=item --version

=item --usage

=item --help

=item --man

Print the usual program information

=back

=head1 DESCRIPTION

Given a specified release, and at least one format, release-mktorrent
creates torrent files for each format of the release.

Fails with a non-zero exitcode when a specified format does not exist.

=head1 BUGS

=over

=back

=head1 COPYRIGHT & LICENSE

Â©Bryan Matsuo (bryan.matsuo@gmail.com)

This file is part of What.

What is free software: you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation, either version 3 of the License, or
(at your option) any later version.

What is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with What.  If not, see <http://www.gnu.org/licenses/>.

=cut
